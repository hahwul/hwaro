require "../options/init_options"
require "../logger/logger"

module Hwaro
  module Core
    class Init
      def run(options : Options::InitOptions)
        run(options.path, options.force)
      end

      def run(target_path : String, force : Bool = false)
        unless Dir.exists?(target_path)
          Dir.mkdir_p(target_path)
        end

        unless force || Dir.empty?(target_path)
          Logger.error "Directory '#{target_path}' is not empty."
          Logger.error "Use --force to overwrite."
          exit(1)
        end

        Logger.info "Initializing new Hwaro project in #{target_path}..."

        create_directory(File.join(target_path, "content"))
        create_file(File.join(target_path, "content", "index.md"), sample_content)
        create_file(File.join(target_path, "content", "about.md"), sample_about_content)

        create_directory(File.join(target_path, "templates"))
        create_file(File.join(target_path, "templates", "header.ecr"), sample_header)
        create_file(File.join(target_path, "templates", "footer.ecr"), sample_footer)
        create_file(File.join(target_path, "templates", "page.ecr"), sample_page_layout)
        create_file(File.join(target_path, "templates", "section.ecr"), sample_section_layout)

        create_directory(File.join(target_path, "static"))
        create_file(File.join(target_path, "config.toml"), sample_config)

        Logger.success "Done! Run `hwaro build` to generate the site."
        unless target_path == "."
          Logger.info "  cd #{target_path}"
        end
      end

      private def create_directory(path : String)
        unless Dir.exists?(path)
          Dir.mkdir_p(path)
          Logger.action :create, path
        else
          Logger.action :exist, path, :blue
        end
      end

      private def create_file(path : String, content : String)
        unless File.exists?(path)
          File.write(path, content)
          Logger.action :create, path
        else
          Logger.action :exist, path, :blue
        end
      end

      private def sample_content
        <<-CONTENT
        +++
        title = "Welcome to Hwaro"
        +++

        # Hello, Hwaro!

        This is a fresh static site generated by [Hwaro](https://github.com/hahwul/hwaro).

        ## Getting Started

        1. Edit `content/index.md` to change this page.
        2. Add new `.md` files in `content/` to create new pages.
        3. Run `hwaro build` to regenerate the site.
        4. Run `hwaro serve` to preview changes locally.
        CONTENT
      end

      private def sample_about_content
        <<-CONTENT
        +++
        title = "About"
        +++

        # About Us

        This is an about page to demonstrate multiple pages.
        CONTENT
      end

      private def sample_page_layout
        <<-HTML
        <%= render "header" %>
        <main>
          <%= content %>
        </main>
        <%= render "footer" %>
        HTML
      end

      private def sample_section_layout
        <<-HTML
        <%= render "header" %>
        <main>
          <h1><%= page_title %></h1>
          <%= content %>

          <ul class="section-list">
            <%= section_list %>
          </ul>
        </main>
        <%= render "footer" %>
        HTML
      end

      private def sample_header
        <<-HTML
        <!DOCTYPE html>
        <html lang="en">
        <head>
          <meta charset="UTF-8">
          <meta name="viewport" content="width=device-width, initial-scale=1.0">
          <meta name="description" content="<%= site_description %>">
          <title><%= page_title %> - <%= site_title %></title>
          <style>
            body { font-family: -apple-system, BlinkMacSystemFont, "Segoe UI", Roboto, Helvetica, Arial, sans-serif; line-height: 1.6; max-width: 800px; margin: 0 auto; padding: 2rem; color: #333; }
            header { margin-bottom: 2rem; border-bottom: 1px solid #eaeaea; padding-bottom: 1rem; }
            h1, h2, h3 { line-height: 1.2; }
            nav a { margin-right: 1rem; text-decoration: none; color: #0070f3; }
            nav a:hover { text-decoration: underline; }
            footer { margin-top: 3rem; border-top: 1px solid #eaeaea; padding-top: 1rem; color: #666; font-size: 0.9rem; text-align: center; }
            code { background: #f4f4f4; padding: 0.2rem 0.4rem; border-radius: 3px; font-size: 0.9em; }
            ul.section-list { list-style: none; padding: 0; }
            ul.section-list li { margin-bottom: 0.5rem; }
          </style>
        </head>
        <body data-section="<%= page_section %>">
          <header>
            <h3><%= site_title %></h3>
            <nav>
              <a href="<%= base_url %>/">Home</a>
              <a href="<%= base_url %>/about.html">About</a>
            </nav>
          </header>

        HTML
      end

      private def sample_footer
        <<-HTML
          <footer>
            <p>Powered by Hwaro</p>
          </footer>
        </body>
        </html>
        HTML
      end

      private def sample_config
        <<-CONTENT
        title = "My Hwaro Site"
        description = "Welcome to my new Hwaro site."
        base_url = "http://localhost:3000"

        [feeds]
        generate = false
        filename = ""
        type = "rss"
        truncate = 0
        CONTENT
      end
    end
  end
end
